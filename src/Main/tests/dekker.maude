
--- simple parallel language, modified from the description in the following:
--- Clavel. M. et al, All about maude-a high-performance logical framework: how to specify, program and verify systems in rewriting logic, LNCS 4350, 2007

fmod MEMORY is 
    inc INT + QID . 
    sorts Memory Bool? Int? .
    subsorts Bool < Bool? . subsorts Int < Int? .
    op null : -> Int? [ctor] .
    op none : -> Memory [ctor] .
    op __ : Memory Memory -> Memory [ctor assoc comm id: none] .
    op `[_`,_`] : Qid Int? -> Memory [ctor] .
    op _in_ : Qid Memory -> Bool? .
    var Q : Qid . var M : Memory . var N? : Int? .
    eq null + N? = null .
    eq null * N? = null .
    eq Q in [Q,N?] M = true .
endfm

fmod EXPRESSION is 
    inc MEMORY .
    sort Expression .
    subsorts Qid Int? < Expression .
    op _+'_ : Expression Expression -> Expression [ctor] .
    op _-'_ : Expression Expression -> Expression [ctor] .
    op _*'_ : Expression Expression -> Expression [ctor] .
    op eval : Expression Memory -> Int? .
    var Q : Qid .
    var M : Memory .
    vars N N' : Int .
    var N? : Int? .
    vars E E' : Expression .
    eq eval(N?, M) = N? .
    eq eval(Q, [Q, N?] M) = N? .
    ceq eval(Q,M) = null if Q in M =/= true .
    eq eval(E +' E', M) = eval(E,M) + eval(E',M) .
    eq eval(E -' E', M) = eval(E,M) - eval(E',M) .
    eq eval(E *' E', M) = eval(E,M) * eval(E',M) .
endfm

fmod TESTS is 
    inc EXPRESSION .
    sort Test .
    op _=_ : Expression Expression -> Test [ctor] . 
    op _!=_ : Expression Expression -> Test [ctor] . 
    op _<'_ : Expression Expression -> Test [ctor] .
    op eval : Test Memory -> Bool .
    var Q : Qid .
    var M : Memory .
    vars N? N'? : Int? .
    vars E E' : Expression .
    eq eval(E = E', M) = eval(E, M) == eval (E', M) .
    eq eval(E != E', M) = eval(E, M) =/= eval (E', M) .
    eq eval(E <' E', M) = eval(E, M) < eval (E', M) .
endfm

fmod SEQUENTIAL is inc TESTS + EXPRESSION .
    sorts UserStatement LoopingUserStatement Program .
    subsort LoopingUserStatement < UserStatement < Program .
    op skip : -> Program .
    op _;_ : Program Program -> Program [prec 61 assoc id: skip] .
    op _:=_ : Qid Expression -> Program .  
    op if_then_fi : Test Program -> Program .
    op while_do_od : Test Program -> Program .
    op repeat_forever : Program -> Program .

    op choose_|_ : Program Program -> Program .	--- nondeterministic choice
    op request_ : Qid -> Program .	--- acquire lock
    op release_ : Qid -> Program .	--- release lock
endfm

mod PARALLEL is inc SEQUENTIAL + TESTS .
    sorts Pid Process Soup MachineState .
    subsort Process < Soup .
    subsort Int < Pid .
    op `[_`,_`] : Pid Program -> Process [ctor] .
    op empty : -> Soup [ctor] .
    op _|_ : Soup Soup -> Soup [ctor prec 61 assoc comm id: empty] .
    op `{_`,_`} : Soup Memory -> MachineState [ctor] .

    vars P P' R : Program .
    var S : Soup .
    var U : UserStatement .
    var L : LoopingUserStatement .
    vars I J : Pid .
    var M : Memory .
    var Q : Qid .
    vars N? X? : Int? .
    var T : Test .
    var E : Expression .

    --- the definition of execution semantics
    rl [user] : 
        {[I, U ; R] | S, M} 
      => 
        {[I, R] | S, M}	    [metadata "just[exec(I)]"] .

    rl [loop] : 
        {[I, L ; R] | S, M} 
      => 
        {[I, L ; R] | S, M} [metadata "just[exec(I)]"] .

    rl [assign] : 
        {[I, (Q := E) ; R] | S, [Q, X?] M} 
      => 
        {[I, R] | S, [Q,eval(E,[Q, X?] M)] M} [metadata "just[exec(I)]"] .

   crl [assign] : 
        {[I, (Q := E) ; R] | S, M} 
      => 
        {[I, R] | S, [Q,eval(E,M)] M} 
     if Q in M =/= true [metadata "just[exec(I)]"] .

    rl [if] : 
        {[I, if T then P fi ; R] | S, M} 
      =>
        {[I, if eval(T, M) then P else skip fi ; R] | S, M} 
     [metadata "just[exec(I)]"] .

    rl [while] : 
        {[I, while T do P od ; R] | S, M} 
      =>
        {[I, if eval(T,M) then (P ; while T do P od) else skip fi ; R] | S, M} 
     [metadata "just[exec(I)]"] .

    rl [repeat] : 
        {[I, repeat P forever ; R] | S, M} 
      =>
        {[I, P ; repeat P forever ; R] | S, M} 
     [metadata "just[exec(I)]"] .
endm

--- Dekker's algorithm
mod DEKKER is inc PARALLEL .
    subsort Int < Pid .
    op crit : -> UserStatement .
    op rem : -> LoopingUserStatement .
    ops p1 p2 : -> Program .
    op initialMem : -> Memory .
    op initial : -> MachineState .

    eq p1 =
    repeat
        'c1 := 1 ;
        while 'c2 = 1 do
    if 'turn = 2 then
        'c1 := 0 ;
    while 'turn = 2 do skip od ;
        'c1 := 1
    fi
        od ;
        crit ;
        'turn := 2 ;
        'c1 := 0 ;
        rem
    forever .
    eq p2 =
    repeat
        'c2 := 1 ;
        while 'c1 = 1 do
    if 'turn = 1 then
        'c2 := 0 ;
    while 'turn = 1 do skip od ;
        'c2 := 1
    fi
        od ;
        crit ;
        'turn := 1 ;
        'c2 := 0 ;
        rem
    forever .

    --- initial memory and state
    eq initialMem = ['c1, 0] ['c2, 0] ['turn, 1] .
    eq initial = { [1, p1] | [2, p2], initialMem } .
endm

load ltlr-interface

--- predicate descriptions
(mod DEKKER-CHECK is 
    inc SATISFACTION .
    protecting DEKKER .
---    protecting CONTEXT[DEKKER] .  --- for context

    subsort MachineState < State .
--- subsort MachineState < Context$MachineState < StateContext . --- for context

    var M : Memory .  vars R : Program .  var S : Soup .  vars I : Pid .

    ops in-crit in-rem : Pid -> Prop .

  *** process I is in critical section
    eq {[I, crit ; R] | S, M} |= in-crit(I) = true . 
    eq {[I, R] | S, M} |= in-crit(I) = false [owise] . 
  *** process I is in rem section
    eq {[I, rem ; R] | S, M} |= in-rem(I) = true . 
    eq {[I, R] | S, M} |= in-rem(I) = false [owise] . 
endm)

set verbose on .

*** the mutual exclusion property; two process cannot enter the
*** critical section at the same time.
(mc initial |= [] ~ (in-crit(1) /\ in-crit(2)) .)

*** the weak fairness property;
*** If p1 and p2 both get to execute infinitely often, then
*** if p1 is infinitely often out of its rem section, then
*** p1 enters its critical section infinitely often.
(mc initial |=
    []<> exec(1) /\ []<> exec(2) -> []<> ~ in-rem(1) -> []<> in-crit(1)
.)

*** another weak fairness  ( just for processor 2 is enough )
(mc initial |=
    ((<>[] enabled(exec(2)) -> []<> exec(2)) /\ []<> ~ in-rem(1))
    -> ([]<> exec(1) -> []<> in-crit(1)) .)

*** test for weak fairness

--- true
(mc initial |=
    []<> ~ in-rem(1) -> ([]<> exec(1) -> []<> in-crit(1))
 under
    (just : enabled(exec(1)) => exec(1)) ; 
    (just : enabled(exec(2)) => exec(2))
.)

--- true
(mc initial |=
    []<> ~ in-rem(1) -> ([]<> exec(1) -> []<> in-crit(1))
 under
    (just : enabled(exec(2)) => exec(2))
.)

(pfmc initial |= []<> ~ in-rem(1) -> []<> in-crit(1) .)

(pfmc initial |= 
    ([]<> ~ in-rem(1) -> []<> in-crit(1)) /\
    ([]<> ~ in-rem(2) -> []<> in-crit(2)) 
.)

